# Interaction couppling
J = 1

# Function to draw internal energy density
g( x ) = 2 * x * J
k( x ) = 2 * sinh( g(x) ) / ( ( cosh( g(x) ) ) ** 2 )
k_primo( x ) = 2 * ( tanh( g(x) ) ) ** 2 - 1

# data filename
datafile = "./energy_plot"
datafile1 = datafile."_c"

set xlabel "beta"
set ylabel "u(beta)"
set samples 100

plot [0:] - ( 1 + 2 * k_primo( x ) * EllipticK( k(x) ) / pi ) / tanh( g(x) ),\
	datafile.".dat" using 1:2:3 with yerrorbars title "Metropolis",\
	datafile1.".dat" u ($1/2):2:3 with yerrorbars title "Cluster"

